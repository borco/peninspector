set(APP_NAME PenInspector)

set(PROJECT_SOURCES
    ../resources/app.qrc
    canvas.cpp
    canvas.h
    dockwidget.cpp
    dockwidget.h
    main.cpp
    mainwindow.cpp
    mainwindow.h
    penconfig.cpp
    penconfig.h
    penconfigdockwidget.cpp
    penconfigdockwidget.h
    penconfigdockwidget.ui
    penconfigmodel.cpp
    penconfigmodel.h
    peninfo.cpp
    peninfo.h
    peninfodockwidget.cpp
    peninfodockwidget.h
    peninfodockwidget.ui
    pressurehistogramdockwidget.cpp
    pressurehistogramdockwidget.h
    pressurehistogrammodel.cpp
    pressurehistogrammodel.h
    pressurehistorydockwidget.cpp
    pressurehistorydockwidget.h
    pressurehistorymodel.cpp
    pressurehistorymodel.h
    tiltpreview.cpp
    tiltpreview.h
)

set(APP_ICON_RESOURCE_WINDOWS "${CMAKE_SOURCE_DIR}/src/resources/windows/app.rc")

# The MACOSX_BUNDLE_ICON_FILE variable is added to the Info.plist
# generated by CMake. This variable contains the .icns file name,
# without the path.
set(MACOSX_BUNDLE_ICON_FILE icon.icns)

# And the following tells CMake where to find and install the file itself.
set(APP_ICON_MACOS "${CMAKE_SOURCE_DIR}/src/resources/icons/${MACOSX_BUNDLE_ICON_FILE}")
set_source_files_properties(${APP_ICON_MACOS} PROPERTIES
    MACOSX_PACKAGE_LOCATION "Resources")

qt_add_executable(${APP_NAME}
    MANUAL_FINALIZATION
    ${PROJECT_SOURCES}
    ${APP_ICON_MACOS}
    ${APP_ICON_RESOURCE_WINDOWS}
)
# Define target properties for Android with Qt 6 as:
#    set_property(TARGET ${APP_NAME} APPEND PROPERTY QT_ANDROID_PACKAGE_SOURCE_DIR
#                 ${CMAKE_CURRENT_SOURCE_DIR}/android)
# For more information, see https://doc.qt.io/qt-6/qt-add-executable.html#target-creation

target_link_libraries(${APP_NAME} PRIVATE
    Qt6::Charts
    Qt6::QuickWidgets
    Qt6::Widgets
)

set_target_properties(${APP_NAME} PROPERTIES
    MACOSX_BUNDLE_GUI_IDENTIFIER my.example.com
    MACOSX_BUNDLE_BUNDLE_VERSION ${PROJECT_VERSION}
    MACOSX_BUNDLE_SHORT_VERSION_STRING ${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}
    MACOSX_BUNDLE TRUE
    WIN32_EXECUTABLE TRUE
)

install(TARGETS ${APP_NAME}
    BUNDLE DESTINATION .
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR})

qt_finalize_executable(${APP_NAME})

add_custom_target(${APP_NAME}_macdeployqt
    DEPENDS ${APP_NAME}
    COMMAND "${MACDEPLOYQT_EXECUTABLE}" "${CMAKE_CURRENT_BINARY_DIR}/${APP_NAME}.app"
)

add_custom_target(${APP_NAME}_create_dmg
    DEPENDS ${APP_NAME}_macdeployqt
    COMMAND
    ${CREATE_DMG_EXECUTABLE}
    --volname "${APP_NAME} Installer"
    --volicon "${APP_ICON_MACOS}"
    --window-pos 200 120
    --background "${CMAKE_SOURCE_DIR}/src/resources/macos/background.png"
    --window-size 800 400
    --icon-size 100
    --icon "${APP_NAME}.app" 200 190
    --app-drop-link 600 190
    "${APP_NAME}-Installer.dmg"
    "${CMAKE_CURRENT_BINARY_DIR}/${APP_NAME}.app"
)
